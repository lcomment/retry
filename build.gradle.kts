import org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
import org.gradle.api.tasks.testing.logging.TestLogEvent.FAILED
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
	alias(libs.plugins.kotlin.jvm)
	alias(libs.plugins.kotlin.spring) apply false
	alias(libs.plugins.kotlin.jpa) apply false
	alias(libs.plugins.spring.boot) apply false
	alias(libs.plugins.spring.dependency.management)
//	alias(libs.plugins.ktlint)
}

allprojects {
	group = "com.momsitter.user"
	version = "1.0.0"

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply(plugin = "kotlin")
	apply(plugin = "org.springframework.boot")
	apply(plugin = "io.spring.dependency-management")
	apply(plugin = "org.jetbrains.kotlin.jvm")
	apply(plugin = "org.jetbrains.kotlin.plugin.spring")
	apply(plugin = "org.jetbrains.kotlin.plugin.jpa")
//	apply(plugin = "org.jmailen.kotlinter")

	kotlin {
		compilerOptions {
			apiVersion = KotlinVersion.KOTLIN_1_9
			languageVersion = KotlinVersion.KOTLIN_1_9
			jvmTarget.set(JvmTarget.JVM_17)
			freeCompilerArgs = listOf(
				"-Xjsr305=strict",
			)
		}
	}

	dependencies {
		implementation(rootProject.libs.kotlin.stdlib)
		implementation(rootProject.libs.kotlin.reflect)
	}

	tasks.withType<Test> {
		useJUnitPlatform()

		testLogging {
			showExceptions = true
			exceptionFormat = FULL
			showCauses = true
			showStackTraces = true
			events = setOf(FAILED)
		}
	}

	tasks.getByName("bootJar") {
		enabled = false
	}

	tasks.getByName("jar") {
		enabled = true
	}
}
